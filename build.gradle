apply plugin: 'eclipse' 
apply plugin: 'groovy' 
apply plugin: 'maven'

group = 'net.yankus'
version = '0.5'

repositories {
    mavenCentral()
    mavenRepo name: 'sonatype', urls:'https://oss.sonatype.org/content/repositories/releases'
}

configurations.compile.transitive = true

dependencies {
    groovy group: 'org.codehaus.groovy', name: 'groovy', version: '1.8.6'
    compile 'joda-time:joda-time:2.1'
    compile ('org.elasticsearch:elasticsearch-lang-groovy:1.1.0') {
        exclude module: 'groovy-all'
    }
    testCompile group: 'junit', name: 'junit', version: '4.8.2'
}

task listJars << {
    configurations.compile.each { File file -> println file.name }
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from 'build/docs/groovydoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives groovydocJar
    archives sourcesJar
}


if (deploymentOperations) {
    apply plugin: 'signing'

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signPom(deployment) }
     
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                  authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
     
                pom.project {
                    groupId "$group"
                    version "$version"
                    artifactId 'visor'
                    packaging 'jar'
                    name 'Visor'
                    description 'A simplistic bridge API for groovy projects that want to search with ElasticSearch'
                    url 'http://morologous.github.com/visor'
     
                    scm {
                        connection 'scm:git:git@github.com:morologous/visor.git'
                        developerConnection 'scm:git:git@github.com/morologous/visor.git'
                        url 'git@github.com:morologous/visor.git'
                    }
                    
                    licenses {
                       license {
                           name 'The Apache Software License, Version 2.0'
                           url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                           distribution 'repo'
                       }
                    }
     
                    developers {
                        developer {
                            id 'morologous'
                            name 'Jason Yankus'
                        }
                    }
               }
            }
        }
    }
    
}


test {
    if (testall == 'false') {
        exclude '**/*Integration*'
    }
}
